apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'io.fabric'
apply plugin: 'com.google.gms.google-services'

android {

    compileSdkVersion appCompileSdkVersion
    buildToolsVersion appBuildToolVersion

    defaultConfig {
        applicationId "com.ovwvwvo.appinfos"
        minSdkVersion appMinSdkVersion
        targetSdkVersion appTargetSdkVersion
        versionCode appVersionCode
        versionName appVersionName

        manifestPlaceholders = [Umeng_key: umengKey]

        resValue "string", "qqScheme", qqScheme
        buildConfigField 'String', 'qqKey', qqKey
        buildConfigField 'String', 'qqValue', qqValue

        buildConfigField 'String', 'wechatKey', wechatKey
        buildConfigField 'String', 'wechatValue', wechatValue

        buildConfigField 'String', 'sinaKey', sinaKey
        buildConfigField 'String', 'sinaValue', sinaValue
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    signingConfigs {
        releaseSigning {
            storeFile file("keystore/keystore.jks")
            storePassword appStorePassword
            keyAlias appKeyAlias
            keyPassword appKeyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseSigning
        }
        debug {
            signingConfig signingConfigs.releaseSigning
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    productFlavors {
        meizu {}
        qihu {}
        tencent {}
        wandoujia {}
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [Channel_ID: name]
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name == 'release') {
            variant.mergedFlavor.versionCode = gitVersionCode()
            variant.mergedFlavor.versionName = gitVersionName()
        }
    }
}

static def gitVersionCode() {
    def cmd = 'git rev-list HEAD  --count'
    cmd.execute().text.trim().toInteger()
}

static def gitVersionName() {
    def cmd = 'git describe --tags'
    def versionName = cmd.execute().text.trim()

    def pattern = "-(\\d+)-g"
    def matcher = versionName =~ pattern

    if (matcher) {
        versionName = versionName.substring(0, matcher.start() + 1) + versionName.substring(matcher.end())
    }
    return versionName
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':common')
    compile project(':shareLibary')

    apt "com.jakewharton:butterknife-compiler:$butterknifeVersion"
    compile "com.jakewharton:butterknife:$butterknifeVersion"
    compile "io.reactivex:rxandroid:$rxVersion"
    compile "io.reactivex:rxjava:$rxVersion"

    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'cn.ovwvwvo.android:JLibary:0.1.2'
    compile 'org.projectlombok:lombok:1.14.8'
    compile 'com.afollestad.material-dialogs:core:0.9.4.1'

    compile 'com.google.firebase:firebase-ads:10.0.1'

    testCompile 'junit:junit:4.12'
}